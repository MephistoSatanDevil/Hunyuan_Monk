
ACTION_IF FILE_EXISTS_IN_GAME c#mk00.spl BEGIN
	OUTER_SET version_now=200
END ELSE OUTER_SET version_now=0

COPY_ALL_GAM_FILES
	READ_LONG 0x20 player_offset
	READ_LONG 0x24 player_count
	FOR (i=0;i<player_count;++i) BEGIN
		npc_offset=player_offset+i*0x160
		READ_LONG npc_offset+0x4 cre_offset
		READ_LONG cre_offset+0x8 name_strref
		GET_STRREF name_strref name
		READ_LONG cre_offset+0x244 kit
		// spells
		PATCH_FOR_EACH type IN 0x2a0 0x2b0 BEGIN
			READ_LONG cre_offset+type spell_offset
			SET spell_offset+=cre_offset
			READ_LONG cre_offset+type+4 kspell_count
			FOR (j=0;j<kspell_count;++j) BEGIN
				READ_ASCII spell_offset + j*0xc spell_name
				PATCH_IF (~%spell_name%~ STRING_CONTAINS_REGEXP ~c#mk.+~)=0 BEGIN
					INNER_PATCH_SAVE spell_name ~%spell_name%~ BEGIN
						WRITE_BYTE 0x1 52
					END
					WRITE_ASCIIE (spell_offset + j*0xc) ~%spell_name%~ (8)
					SET version_now=200
				END
			END
		END
		// effects
		READ_LONG cre_offset+0x2c4 effect_offset
		effect_offset+=cre_offset -0x8
		READ_LONG cre_offset+0x2c8 effect_count
		FOR (j=0;j<effect_count;++j) BEGIN
			READ_LONG effect_offset + j*0x108 +0x10 opcode
			READ_ASCII effect_offset + j*0x108 +0x30 resource
			READ_ASCII effect_offset + j*0x108 +0x94 parent_resource
			READ_ASCII effect_offset + j*0x108 +0xa8 variable (32)
			READ_LONG effect_offset + j*0x108 +0x1c parameter1
			//resource
			PATCH_IF (~%resource%~ STRING_CONTAINS_REGEXP ~c#mk.+~)=0 BEGIN
				INNER_PATCH_SAVE resource ~%resource%~ BEGIN
					WRITE_BYTE 0x1 52
				END
				WRITE_ASCIIE (effect_offset + j*0x108 +0x30) ~%resource%~
				SET version_now=200
			END
			//parent_resource
			PATCH_IF (~%parent_resource%~ STRING_CONTAINS_REGEXP ~c#mk.+~)=0 BEGIN
				INNER_PATCH_SAVE parent_resource ~%parent_resource%~ BEGIN
					WRITE_BYTE 0x1 52
				END
				WRITE_ASCIIE (effect_offset + j*0x108 +0x94) ~%parent_resource%~
				SET version_now=200
			END
			// read version
			PATCH_IF (~%variable%~ STRING_CONTAINS_REGEXP ~c4MonkVersion~)=0
				AND version_now > 200
			BEGIN
				SET version_now=parameter1
			END
		END
	END
BUT_ONLY

ACTION_IF version_now=200 BEGIN
	INCLUDE ~%VERSION_CHECK%/200.tph~
END
